.title	fisee - file seek
.include "lib:crt.mac"
fbdef$
smini$

.macro	iop2b$	r0,r1,err	;r0=hop, r1=lop	
	bpsw	#^o177000,r0,err ;out of range 
	psh	r1
	asr	r0
	ror	r1
	swab	r0
	swab	r1
	bisb	r1,r0
	pop	r1
	bic	#^c511.,r1	;r0=block, r1=byte
.endm

code	fi_see (*FILE, byt:LONG) int

;	o Standard C returns 0=fine, n=fail
;	o Rider returns 0=fail, n=fine
;	o P2/P3 gets the DEC standard long in two words (high/low order)

  proc	fi.see	<r4>
	p1	fil,r4
	p2	hop,r0
	p3	lop,r1
	bpsw	#fbMAN$,(r4),20$	; manual
	iop2b$	r0,r1,10$		; r0=blk, r1=byt, 10$=>overflow
	call	bs$see			; it does the work
	bcs	10$			; something happened
	mov	(pc)+,r0		; fine
10$:	clr	r0			; fail
	br	30$			;
20$:	call	@fb.man(r4) r4,#fbSEE.,r0,r1
30$:	end

.end
