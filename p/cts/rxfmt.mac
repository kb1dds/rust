.title	rxfmt - format rad50 file spec
.include "lib:crt.mac"
smini$

ccstr$
r$xmac:	.ascii	/$.??/
r$xspc:	.asciz	"%r%:%r%r%.%r"
cccod$

code	rx_spc (*WORD, *char) *char

;	rx_spc - convert rad50 spec to RT-11 ascii spec
;	rt_spc - convert RT-11 ascii spec to rad50

  proc	rx.spc
	p1	wrd
	p2	str
	call	rx.fmt #r$xspc,wrd(sp),str(sp)
  end


code	rx_fmt (*char, *WORD, *char) *char

;	%r	next rad50 word
;	%m	macro extension set
;
;	"%r%:%r%r%.%r"

  proc	rx.fmt	<r2,r3,r4>
	p1	fmt,r3
	p2	wrd,r4
	p3	str,r0
	mov	#r$xfil,r2		; assume file output
10$:	movb	(r3)+,r1		; next byte
	beq	70$			; all over
	bneb	r1,#'%,60$		;	
	bneb	(r3),#'r,20$		;
	mov	(r4)+,r1		; word to unpack
	call	rx$unp			; unpack next
	br	50$			;
20$:	bneb	(r3),#'m,30$		; not macro character set
	mov	#r$xmac,r2		; macro character set
	br	50$			;
30$:	bneb	(r3),#':,40$		; not %:
	bnew	-2(r4),10$		; follows device spec
	br	50$			; no device, no colon
40$:	bneb	(r3),#'.,60$		; not %.
	bnew	(r4),10$		; precedes type
50$:	inc	r3			;
	br	10$			;
60$:	movb	r1,(r0)+		;
	br	10$			;
70$:	clrb	(r0)			; terminate it
	end

.end
